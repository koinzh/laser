# wg_2D_ModeProperties_function.lsf

# Calculate some waveguide properties:
# 
# - Effective Mode Area
# - Optical confinement factor (based on energy density)
# - Optical confinement factor (based on E-field intensity)


##########################################################################################
# Effective mode area
#  modified from http://docs.lumerical.com/en/layout_analysis_effective_mode_area.html
#  modified by Lukas Chrostowski, 2015, for MODE Solutions.

switchtolayout;
setanalysis('wavelength',wavelength+1e-9);
run; mesh;
m = "FDE::data::material";
f1 = getdata(m,"f");
eps1 = pinch(getdata(m,"index_x"))^2;  # Assume material is isotropic
z = getdata(m,"z");
y = getdata(m,"y");

switchtolayout;
setanalysis('wavelength',wavelength-1e-9);
run; mesh;
f3 = getdata(m,"f");
eps3 = pinch(getdata(m,"index_x"))^2;  # Assume material is isotropic

findmodes; selectmode(modeNum);  
m = "FDE::data::mode"+num2str(modeNum);
H2 = pinch(getmagnetic(m));
E2 = pinch(getelectric(m));

re_depsdw = real((f3*eps3-f1*eps1)/(f3-f1));
W = 0.5*(re_depsdw*eps0*E2+mu0*H2);

modalarea1 = integrate(W,1:2,y,z)/max(W);

?"Effective mode area: " +num2str(modalarea1*1e12)+" um^2";


##########################################################################################
# Calculate confinement factor, based on energy density
#  modified from http://docs.lumerical.com/en/layout_analysis_effective_mode_area.html
#  lukas chrostowski, 2015
#  kyle murray, 2014

# core region definition
ymin = -width_ridge/2;
zmin = 0;
ymax = width_ridge/2;
zmax = thick_Si;

# Normalize to unit integral
W = W/integrate(W,1:2,y,z);

# Zero E-field outside of waveguide core
Wc = W;
y_index_min = find(y,ymin);
y_index_max = find(y,ymax);
z_index_min = find(z,zmin);
z_index_max = find(z,zmax);
Wc(1:length(y),1:z_index_min) = 0;
Wc(1:length(y),z_index_max:length(z)) = 0;
Wc(1:y_index_min,1:length(z)) = 0;
Wc(y_index_max:length(y),1:length(z)) = 0;

confinement = integrate(Wc,1:2,y,z);

?"Confinement factor (Energy density): " +num2str(confinement); 

# Plot total energy density and confined profiles
image(y*1e9,z*1e9,W/max(W),'y [nm]','z [nm]','W');
image(y*1e9,z*1e9,Wc/max(W),'y [nm]','z [nm]','W core');


##########################################################################################
# Calculate confinement factor, based on E-Field intensity (typically in a laser)
#  modified from http://docs.lumerical.com/en/layout_analysis_effective_mode_area.html
#  lukas chrostowski, 2011, 2015
#  kyle murray, 2014

# core region definition
ymin = -width_ridge/2;
zmin = 0;
ymax = width_ridge/2;
zmax = thick_Si;

# Get squared electric field profile
m = "FDE::data::mode"+num2str(modeNum);
E2 = pinch(getelectric(m));
# Normalize to unit integral
m = "FDE::data::material";
z = getdata(m,"z");
y = getdata(m,"y");
E2 = E2/integrate(E2,1:2,y,z);

# Zero E-field outside of waveguide core
EC2 = E2;
y_index_min = find(y,ymin);
y_index_max = find(y,ymax);
z_index_min = find(z,zmin);
z_index_max = find(z,zmax);
EC2(1:length(y),1:z_index_min) = 0;
EC2(1:length(y),z_index_max:length(z)) = 0;
EC2(1:y_index_min,1:length(z)) = 0;
EC2(y_index_max:length(y),1:length(z)) = 0;

confinement = integrate(EC2,1:2,y,z);
?"Confinement factor (E-Field intensity): " +num2str(confinement); 

# Plot total E-field intensity and confined intensity profiles
image(y*1e9,z*1e9,E2/max(E2),'y [nm]','z [nm]','E-field intensity');
image(y*1e9,z*1e9,EC2/max(E2),'y [nm]','z [nm]','E-field intensity inside core');

